plugins {
    id 'groovy'
    id 'idea'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.9.7'
}

group = 'org.gradle.java'
version = '0.1.1'

targetCompatibility = '1.7'
sourceCompatibility = '1.7'

// Fix a bad interaction with IntelliJ and Gradle > 4.0
idea.module.inheritOutputDirs = true

// make the publishing plugin skip checks that disallow publishing to com.gradle / org.gradle groups
System.setProperty('gradle.publish.skip.namespace.check', 'true')

repositories {
    jcenter()
}

dependencies {
    testCompile ('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude module: 'groovy-all'
    }
}

gradlePlugin {
    plugins {
        experimentalJigsawPlugin {
            id = "org.gradle.java.experimental-jigsaw-junit5"
            implementationClass = "org.gradle.java.JigsawPlugin"
        }
    }
}

pluginBundle {
    website = 'https://guides.gradle.org/building-java-9-modules'
    vcsUrl = 'https://github.com/gradle/gradle-java-modules'
    plugins {
        experimentalJigsawPlugin {
            id = 'org.gradle.java.experimental-jigsaw-junit5'
            displayName = 'Experimental Jigsaw Plugin with Junit5'
            description = 'Experiment with Java 9 modules before they are officially supported.'
            tags = ['jigsaw', 'modules', 'java9', 'junit5']
            version = project.version
        }
    }
}
